[function test_dialogs_client]
serv.smsg "test_dialogs_client\n"

dialog(d_test_dialogs_client,first,second,third)

serv.smsg "\n"
src.sysmessage("test_dialogs_client done")

[dialog d_test_dialogs_client]
ShouldBeSameString("args contains only first argument","first",<args>)
ShouldBeSameString("argv(0)","first",<argv(0)>)
ShouldBeSameString("argv(1)","second",<argv(1)>)
ShouldBeSameString("argv(2)","third",<argv(2)>)

ShouldBeSameString("argo.cont is the same as src",<src>,<argo.cont>)
ShouldBeSameString("argo.cont chained r_WriteVal",<src.name>,<argo.cont.name>)
argo.cont.tag(test_dialog_cont_cont_tag,123)
ShouldBeSameString("argo.cont chained r_Verb",123,<argo.cont.tag(test_dialog_cont_cont_tag)>)
argo.cont.test_dialog_cont_cont
ShouldBeSameString("argo.cont chained call",321,<argo.cont.tag(test_dialog_cont_cont_tag)>)

argo.tag(tag1,123)
ShouldBeSameString("can get/set argo tag",123,<argo.tag(tag1)>)

tag(tag1,124)
ShouldBeSameString("can get/set unprefixed tag",124,<tag(tag1)>)
ShouldBeSameString("unprefixed is the same as src tag",124,<src.tag(tag1)>)

src.tag(tag1,321)
ShouldBeSameString("can get/set src tag",321,<src.tag(tag1)>)
ShouldBeSameString("src is the same as unprefixed tag",321,<src.tag(tag1)>)
ShouldBeSameString("unprefixed and src tags doesn't change argo tag",123,<argo.tag(tag1)>)

argo.tag(test_dialog_core_argo_tag,333)
argo.test_dialog_core_argo

argo.tag.remove(test_dialog_core_argo_tag)
ShouldBeSameString("can remove argo tag","","<argo.tag(test_dialog_core_argo_tag)>")

argo.tag(test_dialog_core_argo2_tag,222)
arg(result,<argo.test_dialog_core_argo2>)

[function test_dialog_core_argo]
ShouldBeSameString("r_writeVal runs on dialog instance first",333,<eval tag(test_dialog_core_argo_tag)>)

tag(index,123)
tag(indexedTag[123],"indexedTagValue")
ShouldBeSameString("r_writeVal evaluates tag index inside argo context",indexedTagValue,<tag(indexedTag[tag(index)])>)

tag(indexedTag2[tag(index)],"indexedTag2Value")
ShouldBeSameString("r_verb evaluates tag index inside argo context",indexedTag2Value,<tag(indexedTag2[123])>)

[function test_dialog_core_argo2]
ShouldBeSameString("r_writeVal runs on dialog instance first - invoked from argo.r_writeVal",222,<eval tag(test_dialog_core_argo2_tag)>)

[function test_dialog_cont_cont]
tag(test_dialog_cont_cont_tag,321)
