[function test_events]
serv.smsg "test_events\n"

ShouldBeSameString("no event on char",0,<finduid(010000).isevent(e_testevent)>)
finduid(010000).events +e_testevent
ShouldBeSameString("event on char",1,<finduid(010000).isevent(e_testevent)>)
ShouldBeSameString("event on char without parentheses",1,<finduid(010000).isevent e_testevent>)

var(customTriggerVar,0)
finduid(010000).trigger @customTrigger
ShouldBeSameString("triggers custom trigger on event - without parentheses, with at",customTriggerExecuted,<var(customTriggerVar)>)

var(customTriggerVar,0)
finduid(010000).trigger(@customTrigger2)
ShouldBeSameString("triggers custom trigger on event - with parentheses, at",customTrigger2Executed,<var(customTriggerVar)>)

var(builtinTriggerVar,0)
finduid(010000).trigger(death)
ShouldBeSameString("triggers builtin trigger on event - with parentheses without at",deathExecuted,<var(builtinTriggerVar)>)

finduid(010000).events -e_testevent
ShouldBeSameString("removed event on char",0,<finduid(010000).isevent(e_testevent)>)

finduid(010000).events(+e_testevent)
ShouldBeSameString("event on char - subscribe with parentheses",1,<finduid(010000).isevent(e_testevent)>)
finduid(010000).events(-e_testevent)
ShouldBeSameString("event on char - unsubscribe with parentheses",0,<finduid(010000).isevent(e_testevent)>)

finduid(010000).events(+e_testevent)
finduid(010000).trigger(@triggerexplicit)
finduid(010000).events(-e_testevent)

finduid(010000).newitem(i_event_test)

var(builtinTriggerVar,0)
finduid(010000).equip(<lastnew>)
ShouldBeSameString("builtin event on item - implicit",equipExecuted,<var(builtinTriggerVar)>)

var(builtinTriggerVar,0)
lastnew.trigger(equip)
ShouldBeSameString("builtin event on item - explicit with parentheses without at",equipExecuted,<var(builtinTriggerVar)>)

// This doesn't work on Sphere 0.99
// var(builtinTriggerVar,0)
// lastnew.trigger(@equip)
// ShouldBeSameString("builtin event on item - explicit with parentheses, at",equipExecuted,<var(builtinTriggerVar)>)

lastnew.remove

serv.smsg "\n"

[itemdef i_backpack_event_test]
id=i_backpack
name=bakcpack event test

on=@stackon
serv.smsg("@stackon")
return 1

[events e_testevent]
on=@skillstart
on=@customTrigger
var(customTriggerVar,customTriggerExecuted)

on=@customTrigger2
var(customTriggerVar,customTrigger2Executed)

on=@triggerexplicit
arg(local1,123)
ShouldBeSameString("local variables in explicit trigger",123,<arg(local1)>)

on=@death
var(builtinTriggerVar,deathExecuted)

[ITEMDEF 0f51]
DEFNAME=i_event_test
TYPE=T_WEAPON_FENCE
FLIP=1
DAM=3
//SPEED=80
SKILL=fencing
REQSTR=1
TWOHANDS=0
LAYER=1
//MATERIAL=20
WEIGHT=30
CATEGORY=Provisions - Weapons
SUBSECTION=Knives & Daggers
DESCRIPTION=Dagger
SKILLMAKE=BLACKSMITHING 26.7
DUPELIST=0f52

on=@equip
arg(local1,123)
ShouldBeSameString("local variables in implicit trigger",123,<arg(local1)>)
var(builtinTriggerVar,equipExecuted)

[events e_allplayers]
on=@login
if (finduid(<src>).isChar)
    tst_success("@login - src set: <src>")
else
    tst_fail("@login - src not set!")
endif

if (isChar)
    tst_success("@login - current object is char")
else
    tst_fail("@login - current object is char")
endif

if (<uid> == <src.uid>)
    tst_success("@login - src and current object are same: <uid>, <src.uid>")
else
    tst_fail("@login - src and current object are same: <uid>, <src.uid>")
endif

tag(testTag1,12)
tag(testTag2,4)
ShouldBeSameString("@login - can eval char tags",20,<eval tag(testTag1)+<eval tag(testTag2)*2>>)

on=@userdclick
if (finduid(<src>).isChar)
    tst_success("@userdclick - src set: <src>")
else
    tst_fail("@userdclick - src set: <src>")
endif

on=@userclick
if (finduid(<src>).isChar)
    tst_success("@userclick - src set: <src>")
else
    tst_fail("@userclick - src set: <src>")
endif

on=@step
if (finduid(<src>).isChar)
    tst_success("@step - src set: <src>")
else
    tst_fail("@step - src set: <src>")
endif
